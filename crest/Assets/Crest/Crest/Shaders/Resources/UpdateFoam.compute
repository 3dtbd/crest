// Crest Ocean System

// This file is subject to the MIT License as seen in the root of this folder structure (LICENSE)

#pragma kernel UpdateFoam

#include "../OceanLODData.hlsl"

float _FoamFadeRate;
float _WaveFoamStrength;
float _WaveFoamCoverage;
float _ShorelineFoamMaxDepth;
float _ShorelineFoamStrength;
float _SimDeltaTime;
float _SimDeltaTimePrev;
float _LODChange;

RWTexture2D<float> _LD_Texture_Target;

[numthreads(THREAD_GROUP_SIZE_X,THREAD_GROUP_SIZE_Y,1)]
void UpdateFoam(uint3 id : SV_DispatchThreadID)
{
	// lod data 1 is current frame, compute world pos from quad uv
	const float sliceIndex = _LD_SliceIndex;
	const float sliceIndexSource = sliceIndex + _LODChange;
	float width; float height;
	{
		_LD_Texture_Foam_Source.GetDimensions(width, height);
	}
	const float2 uv_target = IDtoUV(id.xy, width, height);
	const float2 worldPosXZ = UVToWorld(uv_target, sliceIndex);
	const float2 uv_source = WorldToUV_Source(worldPosXZ, sliceIndexSource);
	// #if _FLOW_ON
	half2 velocity = _LD_Texture_Flow.SampleLevel(LODData_linear_clamp_sampler, uv_target, 0).xy;
	half foam = _LD_Texture_Foam_Source.SampleLevel(LODData_linear_clamp_sampler, uv_source
		- ((_SimDeltaTime * _LD_Params_Source[sliceIndexSource].w) * velocity), 0
		).x;
	// #else
	// // sampler will clamp the uv currently
	// half foam = tex2Dlod(_LD_Texture_Foam_Source, uv_source).x;
	// #endif

	half2 r = abs(uv_source.xy - 0.5);

	if (
		(sliceIndexSource < 0) || /* (sliceIndexSource > depth) || TODO(SRT): Ensure this will work correctly */
		(max(r.x, r.y) > 0.5 - _LD_Params_Source[sliceIndexSource].w)
	) {
		// no border wrap mode for RTs in unity it seems, so make any off-texture reads 0 manually
		foam = 0.0;
	}

	// fade
	foam *= max(0.0, 1.0 - _FoamFadeRate * _SimDeltaTime);

	// sample displacement texture and generate foam from it
	const float3 dd = float3(_LD_Params[sliceIndex].w, 0.0, _LD_Params[sliceIndex].x);
	half3 s = SampleLod(_LD_Texture_AnimatedWaves, uv_target).xyz;
	half3 sx = SampleLodLevel(_LD_Texture_AnimatedWaves, uv_target + dd.xy, dd.y).xyz;
	half3 sz = SampleLodLevel(_LD_Texture_AnimatedWaves, uv_target + dd.yx, dd.y).xyz;
	float3 disp = s.xyz;
	float3 disp_x = dd.zyy + sx.xyz;
	float3 disp_z = dd.yyz + sz.xyz;
	// The determinant of the displacement Jacobian is a good measure for turbulence:
	// > 1: Stretch
	// < 1: Squash
	// < 0: Overlap
	float4 du = float4(disp_x.xz, disp_z.xz) - disp.xzxz;
	float det = (du.x * du.w - du.y * du.z) / (_LD_Params[sliceIndex].x * _LD_Params[sliceIndex].x);
	foam += 5.0 * _SimDeltaTime * _WaveFoamStrength * saturate(_WaveFoamCoverage - det);

	// add foam in shallow water. use the displaced position to ensure we add foam where world objects are.
	float2 uv_displaced = WorldToUV(worldPosXZ + disp.xz, sliceIndex);
	float signedOceanDepth = SampleLodLevel(_LD_Texture_SeaFloorDepth, uv_displaced, 1).x + disp.y;
	foam += _ShorelineFoamStrength * _SimDeltaTime * saturate(1.0 - signedOceanDepth / _ShorelineFoamMaxDepth);

	_LD_Texture_Target[id.xy] = foam;
}
